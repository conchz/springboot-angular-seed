group 'com.github.lavenderx'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE'
        classpath 'com.bmuschko:gradle-docker-plugin:2.6.8'
    }
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-java-application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

mainClassName = 'com.github.lavenderx.ApplicationBoot'

configurations {
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }

        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

ext {
    springbootVersion = '1.3.5.RELEASE'

    appVersion = version.toString().replaceAll("-(?i)SNAPSHOT", "")
    serverPort = 'server.port'

    def props = new Properties()
    file('src/main/resources/common-config.properties').withInputStream {
        props.load(it)
    }
    props.each {
        if ("$serverPort" == "$it.key") {
            serverPort = it.value
        }
    }
}

bootRun {
    // ex: gradle bootRun -PjvmArgs="-Dspring.profiles.active=dev"
    if (project.hasProperty('jvmArgs')) {
        jvmArgs = project.jvmArgs.split("\\s+") as List
    }

    addResources = true
}

docker {
    javaApplication {
        baseImage = 'dockerfile/java:8-jre-alpine'
        maintainer = 'Zongzhi Bai "dolphineor@gmail.com"'
        port = "$serverPort".toInteger()
        tag = "lavenderx/$project.name:$appVersion"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'com.google.guava:guava:19.0'
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    compile 'org.reflections:reflections:0.9.10'
    compile("org.springframework.boot:spring-boot-starter-web:$springbootVersion") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile "org.springframework.boot:spring-boot-starter-undertow:$springbootVersion"
    compile "org.springframework.boot:spring-boot-starter-security:$springbootVersion"
    compile "org.springframework.boot:spring-boot-starter-mustache:$springbootVersion"
    compile "org.springframework.boot:spring-boot-starter-amqp:$springbootVersion"
    compile "org.springframework.boot:spring-boot-devtools:$springbootVersion"
    compile 'org.springframework.data:spring-data-mongodb:1.8.4.RELEASE'

    // Webjars dependencies
    compile 'org.webjars:webjars-locator:0.31'
    compile 'org.webjars:requirejs:2.2.0'
    compile 'org.webjars:jquery:2.2.3' // for bootstrap
    compile('org.webjars:angularjs:1.5.5') {
        exclude group: 'org.webjars', module: 'jquery'
    }
    compile('org.webjars:angular-ui-bootstrap:1.3.2') {
        exclude group: 'org.webjars', module: 'jquery'
    }
    compile 'org.webjars:ui-grid:3.1.1'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springbootVersion"
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// Package sources into a executable binary distribution zip file

applicationDefaultJvmArgs = [
        '-server',
        '-Xms1g',
        '-Xmx1g',
        '-Dfile.encoding=UTF-8'
]

task copyLicense {
    outputs.file new File("$buildDir/LICENSE")
    doLast {
        copy {
            from 'LICENSE'
            into "$buildDir"
        }
    }
}

applicationDistribution.from(copyLicense) {
    into ""
}

task copyDistZip(type: Copy) {
    dependsOn 'distZip'

    from("build/distributions/${project.name}-${project.version}.zip") {
        rename { String fileName ->
            fileName.replace("-${project.version}", "")
        }
    }
    from 'src/main/docker'
    into 'build/docker'

    includeEmptyDirs = false
}

build.dependsOn copyDistZip
